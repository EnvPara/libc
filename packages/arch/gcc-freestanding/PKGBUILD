_target=x86_64-elf-redox
pkgname=$_target-gcc-freestanding-git
pkgver=r152183.98e77b0b0d2
pkgrel=1
arch=(i686 x86_64)
license=(GPL)
source=("git+https://github.com/redox-os/gcc#branch=redox")
md5sums=('SKIP')
makedepends=('autoconf-2.64' 'git')
depends=($_target-binutils-git libmpc zlib)

prepare() {
  cd "$srcdir/gcc"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
  rm -rf $srcdir/gcc-build
  mkdir $srcdir/gcc-build
}

build() {
  cd "$srcdir/gcc"

  cd libstdc++-v3
  /opt/autoconf/2.64/bin/autoconf
  cd ..

  cd $srcdir/gcc-build
  $srcdir/gcc/configure --target=$_target --prefix=/usr --disable-nls --enable-languages=c,c++ --without-headers
  make all-gcc all-target-libgcc
}

package() {
  cd $srcdir/gcc-build

  make DESTDIR="$pkgdir" install-gcc install-target-libgcc

  rm -r "$pkgdir"/usr/share/man/man7
  rm -r "$pkgdir"/usr/share/info
}

pkgver() {
  cd "$srcdir/gcc"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
